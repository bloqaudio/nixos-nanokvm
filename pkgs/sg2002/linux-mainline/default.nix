# Mainline Linux for the SG2002.
#
# This uses nixpkgs' `linux_latest` source/build machinery with the RISC-V
# defconfig as its small base.  NixOS's generic common config is deliberately
# disabled: it enables thousands of unrelated modules on this 256 MiB SoC.
# Two SG2002-specific pieces are layered on via `.override`:
#
#   - kernelPatches  — the SG2002 SoC-support queue (see ./patches.nix).
#   - structuredExtraConfig — our add/remove deltas (see ./config.nix):
#     turn ON the SoC drivers + gadget stack + AIC8800 OOT bits, turn
#     OFF anything from defconfig that the 256 MB board doesn't want.
#
# This retains nixpkgs' cross-build, patch, config-check, module and output
# handling without inheriting the workstation/server-oriented common config.
# buildLinux applies the patches before generating the config, so
# patch-introduced Kconfig symbols referenced in config.nix resolve
# correctly (the reason the old path needed make-config.nix).
{
  lib,
  fetchurl,
  linux_latest,
  audio ? false,
  bluetooth ? false,
  # nixpkgs re-.override's kernels with `features` / friends; tolerate
  # any extra args callPackage / linuxPackagesFor threads through.
  ...
}:
let
  source = import ./source.nix {inherit fetchurl;};
in
# Standard nixpkgs riscv64 kernel: buildLinux installs the uncompressed
# `Image` (kernelFile default) into $out on its own — no compress/install
# dance needed. We only layer on the SG2002 patch queue + config delta.
(linux_latest.override {
  defconfig = "defconfig";
  enableCommonConfig = false;
  autoModules = false;
  argsOverride = {
    inherit (source) src version modDirVersion;
    extraMeta.branch = "7.2-rc";
  };
  structuredExtraConfig = (import ./config.nix {inherit lib;})
    // lib.optionalAttrs audio {
      # The common Nano carrier DT already describes the internal RXADC on
      # I2S0 and TXDAC on I2S3 as the sg2002-onboard simple card.  Keep the
      # complete, lab-proven minimum built in: a diskless initrd must not
      # depend on broad ALSA codec module discovery.
      SOUND = lib.kernel.yes;
      SND = lib.kernel.yes;
      SND_PCM = lib.kernel.yes;
      SND_SOC = lib.kernel.yes;
      SND_SIMPLE_CARD = lib.kernel.yes;
      SND_SOC_CV1800B_TDM = lib.kernel.yes;
      SND_SOC_CV1800B_ADC_CODEC = lib.kernel.yes;
      SND_SOC_CV1800B_DAC_CODEC = lib.kernel.yes;
    }
    // lib.optionalAttrs bluetooth {
    # AIC8800 FDRV provides HCI_SDIO itself; the kernel needs only the
    # Bluetooth core and the BR/EDR + LE protocols for BlueZ discovery.
    # BNEP is the kernel data path for BlueZ's Bluetooth PAN profile.
    BT = lib.kernel.module;
    BT_BREDR = lib.kernel.yes;
    BT_LE = lib.kernel.yes;
    BT_BNEP = lib.kernel.module;
    BT_BNEP_MC_FILTER = lib.kernel.yes;
    BT_BNEP_PROTO_FILTER = lib.kernel.yes;
  };
  kernelPatches = (import ./patches.nix).patches;
  # Let olddefconfig drop options whose dependencies are unavailable.
  ignoreConfigErrors = true;
})
